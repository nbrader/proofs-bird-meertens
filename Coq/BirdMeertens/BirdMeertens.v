Require Import Coq.Program.Basics.
Require Import Coq.Program.Combinators.
Require Import Coq.Lists.List.

Require Import Coq.ZArith.Int.
Require Import Coq.ZArith.BinInt.
Require Import Coq.Init.Datatypes.

Require Import Coq.Structures.GenericMinMax.
Open Scope Z_scope.

Require Import BirdMeertens.Lemmas.
Require Import BirdMeertens.ListFunctions.

(* Forms of MaxSegSum *)
Definition form1 : list Z -> Z := nonNegMaximum ∘ map nonNegSum ∘ segs.
Definition form2 : list Z -> Z := nonNegMaximum ∘ map nonNegSum ∘ concat ∘ map inits ∘ tails.
Definition form3 : list Z -> Z := nonNegMaximum ∘ concat ∘ map (map nonNegSum) ∘ map inits ∘ tails.
Definition form4 : list Z -> Z := nonNegMaximum ∘ map nonNegMaximum ∘ map (map nonNegSum) ∘ map inits ∘ tails.
Definition form5 : list Z -> Z := nonNegMaximum ∘ map (nonNegMaximum ∘ map nonNegSum ∘ inits) ∘ tails.
Definition form6 : list Z -> Z := nonNegMaximum ∘ map (fold_right nonNegPlus 0) ∘ tails.
Definition form7 : list Z -> Z := nonNegMaximum ∘ scan_right nonNegPlus 0.
Definition form8 : list Z -> Z := fst ∘ fold_right maxSoFarAndPreviousSum (0, 0).

Theorem form1_eq_form2 : form1 = form2.
Proof.
  reflexivity.
Qed.

Theorem form2_eq_form3 : form2 = form3.
Proof.
  unfold form2.
  unfold form3.
  f_equal.
  rewrite compose_assoc.
  rewrite (compose_assoc _ _ _ _ (concat ∘ map inits) (map nonNegSum) nonNegMaximum).
  rewrite <- (compose_assoc _ _ _ _ (map inits) concat (map nonNegSum)).
  rewrite (map_promotion nonNegSum).
  reflexivity.
Qed.

Theorem form3_eq_form4 : form3 = form4.
Proof.
  unfold form3.
  unfold form4.
  rewrite compose_assoc.
  rewrite fold_promotion.
  reflexivity.
Qed.

Theorem form4_eq_form5 : form4 = form5.
Proof.
  unfold form4.
  unfold form5.
  f_equal.
  rewrite compose_assoc.
  rewrite compose_assoc.
  rewrite (map_distr (map nonNegSum) inits).
  rewrite (map_distr nonNegMaximum (compose (map nonNegSum) inits)).
  reflexivity.
Qed.

Theorem form5_eq_form6 : form5 = form6.
Proof.
  unfold form5.
  unfold form6.
  f_equal.
  f_equal.
  f_equal.
  (* apply horners_rule. *)
Admitted.

Theorem MaxSegSum_Equivalence : form1 = form6.
Proof.
  rewrite form1_eq_form2.
  rewrite form2_eq_form3.
  rewrite form3_eq_form4.
  rewrite form4_eq_form5.
  rewrite form5_eq_form6.
  reflexivity.
Qed.
